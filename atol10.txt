// https://integration.atol.ru/api/?pascal#primer-ispolzovaniya

Процедура ОсновныеДействияФормыПробитьЧек(Кнопка)
	
	Если НомерЧекаККМ <> 0 Тогда
		Предупреждение("Чек уже пробит на фискальном регистраторе!");
		Возврат;
	КонецЕсли;
	
	//Если (НЕ Проведен) ИЛИ Модифицированность Тогда
	//	Ответ = Вопрос("Провести документ?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	//	Если Ответ <> КодВозвратаДиалога.Да Тогда
	//		Предупреждение("Операция не выполнена!");
	//		Возврат;
	//	КонецЕсли;
	//КонецЕсли;
	
	ОбъектДрайвера = Новый COMобъект("AddIn.Fptr10");
	
	// Подключение по порту USB
	ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_PORT, Строка(ОбъектДрайвера.LIBFPTR_PORT_USB));
	//ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_USB_DEVICE_PATH, "");
    ОбъектДрайвера.applySingleSettings();
	
	// Подключение по COM порту, для Window "COM5" или просто "5", для Linux "/dev/ttyS5" или "/dev/ttyACM0"
	//ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_MODEL, Строка(ОбъектДрайвера.LIBFPTR_MODEL_ATOL_AUTO));
	//ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_PORT, Строка(ОбъектДрайвера.LIBFPTR_PORT_COM));
	//ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_COM_FILE, "COM5");
	//ОбъектДрайвера.setSingleSetting(ОбъектДрайвера.LIBFPTR_SETTING_BAUDRATE, Строка(ОбъектДрайвера.LIBFPTR_PORT_BR_115200));
	//ОбъектДрайвера.applySingleSettings();
	

    // Соединение с ККТ
    ОбъектДрайвера.open();
	
	// Состояние смены
	ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_DATA_TYPE, ОбъектДрайвера.LIBFPTR_DT_SHIFT_STATE);
	ОбъектДрайвера.queryData();
	
	ОбъектДрайвера_СостояниеСмены = ОбъектДрайвера.getParamInt(ОбъектДрайвера.LIBFPTR_PARAM_SHIFT_STATE);
	
	Если ОбъектДрайвера_СостояниеСмены = ОбъектДрайвера.LIBFPTR_SS_CLOSED Тогда
		Сообщить("Кассовая смена не открыта! Откройте смену и повторите попытку.");
		ОбъектДрайвера.beep();
		
		ОбъектДрайвера.close();
		ОбъектДрайвера = Неопределено;
		Возврат;
	ИначеЕсли ОбъектДрайвера_СостояниеСмены = ОбъектДрайвера.LIBFPTR_SS_OPENED Тогда
		// Все отлично смена открыта		
	ИначеЕсли ОбъектДрайвера_СостояниеСмены = ОбъектДрайвера.LIBFPTR_SS_EXPIRED Тогда
		Сообщить("Кассовая смена истекла! Переоткройте смену и повторите попытку.");
		ОбъектДрайвера.beep();
		
		ОбъектДрайвера.close();
		ОбъектДрайвера = Неопределено;
		Возврат;
	Иначе
		Сообщить("Не смогли получить состояние кассовой смены!");
		ОбъектДрайвера.close();
		ОбъектДрайвера = Неопределено;
		Возврат;
	КонецЕсли;
	
	// Регистрация кассира
	ОбъектДрайвера.setParam(1021, СокрЛП(ЭтотОбъект.Ответственный.Наименование));
	ОбъектДрайвера.setParam(1203, СокрЛП(ЭтотОбъект.Ответственный.ИНН));
	ОбъектДрайвера.operatorLogin();
	
	ЗаписатьВФорме(РежимЗаписиДокумента.Проведение);
	
	Если (НЕ Проведен) ИЛИ Модифицированность Тогда
		Предупреждение("Операция не выполнена!");
		ОбъектДрайвера.close();
		ОбъектДрайвера = Неопределено;
		Возврат;
	КонецЕсли;
		
	// Открытие чека
	ОбъектДрайвера_ТипРасчета = "";
	Если ЭтотОбъект.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
        ОбъектДрайвера_ТипРасчета = ОбъектДрайвера.LIBFPTR_RT_SELL ;
	ИначеЕсли ЭтотОбъект.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств Тогда
		ОбъектДрайвера_ТипРасчета = ОбъектДрайвера.LIBFPTR_RT_BUY;
	ИначеЕсли ЭтотОбъект.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
		ОбъектДрайвера_ТипРасчета = ОбъектДрайвера.LIBFPTR_RT_SELL_RETURN;
	ИначеЕсли ЭтотОбъект.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств Тогда
		ОбъектДрайвера_ТипРасчета = ОбъектДрайвера.LIBFPTR_RT_BUY_RETURN;
	КонецЕсли;
	ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_RECEIPT_TYPE, ОбъектДрайвера_ТипРасчета);
	
	// (с передачей электронного адреса или телефона получателя)
	Если Не ПустаяСтрока(ЭтотОбъект.АдресТелефонКлиента) Тогда
		ОбъектДрайвера.setParam(1008, СокрЛП(ЭтотОбъект.АдресТелефонКлиента));
		//ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_RECEIPT_ELECTRONICALLY, Истина);
	КонецЕсли;
	
	// СНО
	ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_DEFAULT;
	Если ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.ОСН Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_OSN;
	ИначеЕсли ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_USN_INCOME;
	ИначеЕсли ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_USN_INCOME_OUTCOME;
	ИначеЕсли ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_ENVD;
	ИначеЕсли ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_ESN;
	ИначеЕсли ЭтотОбъект.СНО = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
		ОбъектДрайвера_ТипСНО = ОбъектДрайвера.LIBFPTR_TT_PATENT;
	КонецЕсли;
	ОбъектДрайвера.setParam(1055, ОбъектДрайвера_ТипСНО);
	
	// Признак агента
	//ОбъектДрайвера.setParam(1057, ОбъектДрайвера.LIBFPTR_AT_NONE);
	
	// Дополнительный реквизит чека (БСО)
	// Длина по ФФД всего 16 символов
	Если Не ПустаяСтрока(ЭтотОбъект.ДополнительныйРеквизит) Тогда
		ОбъектДрайвера.setParam(1192, ЭтотОбъект.ДополнительныйРеквизит);
	КонецЕсли;
	
	
    ОбъектДрайвера.openReceipt();
	
	Для Каждого ТекСтр Из ЭтотОбъект.ПредметыРасчета Цикл
		// Регистрация позиции
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_COMMODITY_NAME, ТекСтр.Наименование);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PRICE, ТекСтр.Цена);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_QUANTITY, ТекСтр.Количество);
		
		ОбъектДрайвера_СтавкаНДС = 0;
		Если ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT18;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT10;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT118;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT110;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT0;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_NO;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT20;
		ИначеЕсли ТекСтр.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT120;
		КонецЕсли;
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_TAX_TYPE, ОбъектДрайвера_СтавкаНДС);
		//ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_TAX_SUM, ТекСтр.СуммаНДС);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_USE_ONLY_TAX_TYPE, Истина);
		
		ОбъектДрайвера_ПризнакПредметаРасчета = 0;
		Если ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 1;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТовар Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 2;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Работа Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 3;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Услуга Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 4;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.СтавкаАзартнойИгры Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 5;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ВыигрышАзартнойИгры Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 6;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ЛотерейныйБилет Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 7;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ВыигрышЛотереи Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 8;                                
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПредоставлениеРИД Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 9;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 10;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.АгентскоеВознаграждение Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 11;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.СоставнойПредметРасчета Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 12;
		ИначеЕсли ТекСтр.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ИнойПредметРасчета Тогда
			ОбъектДрайвера_ПризнакПредметаРасчета = 13;
			
			// дополнить до 18 - Курортный сбор
			// уже до 19 - Залог 
			// http://www.consultant.ru/document/cons_doc_LAW_214339/cfdfbc0cb69618bfb358a7d612a1ac60149a7525/
			// https://online.atol.ru/files/Uproschennaya_versiya_FFD_ot_ATOL.pdf
		КонецЕсли;
		ОбъектДрайвера.setParam(1212, ОбъектДрайвера_ПризнакПредметаРасчета);
		
		ОбъектДрайвера_ПризнакСпособаРасчета = 0; 
		// по умолчанию надо бы 4 при передаче с полной оплатой
		// может лучше при добавлении новых позиций сразу устанавливать нужное?
		Если ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 1;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 2;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.Аванс Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 3;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 4;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 5;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 6;
		ИначеЕсли ТекСтр.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ОплатаКредита Тогда
			ОбъектДрайвера_ПризнакСпособаРасчета = 7;
		КонецЕсли;
		ОбъектДрайвера.setParam(1214, ОбъектДрайвера_ПризнакСпособаРасчета);
		
		ОбъектДрайвера.registration();
	КонецЦикла;
	
	// Регистрация итога (можно округлить копейки, не обязательно)
	//ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_SUM, 369.0);
	//ОбъектДрайвера.receiptTotal;
	
	// Ставка налога целиком на чек
	тзНалоги = ЭтотОбъект.ПредметыРасчета.Выгрузить();
	тзНалоги.Свернуть("СтавкаНДС", "СуммаНДС");
	Для Каждого ТекСтрНалоги Из тзНалоги Цикл
		ОбъектДрайвера_СтавкаНДС = 0;
		Если ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT18;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT10;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT118;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT110;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT0;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_NO;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT20;
		ИначеЕсли ТекСтрНалоги.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
			ОбъектДрайвера_СтавкаНДС = ОбъектДрайвера.LIBFPTR_TAX_VAT120;
		КонецЕсли;
		
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_TAX_TYPE, ОбъектДрайвера_СтавкаНДС);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_TAX_SUM, ТекСтрНалоги.СуммаНДС);
		ОбъектДрайвера.receiptTax();
	КонецЦикла;

    // Оплата
	// Наличными
	Если ЭтотОбъект.СуммаНаличными > 0 Тогда
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_TYPE, ОбъектДрайвера.LIBFPTR_PT_CASH);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_SUM, ЭтотОбъект.СуммаНаличными);
		ОбъектДрайвера.payment();
	КонецЕсли;
	// Безнал
	Если ЭтотОбъект.СуммаБезналичными > 0 Тогда
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_TYPE, ОбъектДрайвера.LIBFPTR_PT_ELECTRONICALLY);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_SUM, ЭтотОбъект.СуммаБезналичными);
		ОбъектДрайвера.payment();
	КонецЕсли;
	// Предоплата
	Если ЭтотОбъект.СуммаПредоплатой > 0 Тогда
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_TYPE, ОбъектДрайвера.LIBFPTR_PT_PREPAID);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_SUM, ЭтотОбъект.СуммаПредоплатой);
		ОбъектДрайвера.payment();
	КонецЕсли;
	// Постоплата
	Если ЭтотОбъект.СуммаПостоплатой > 0 Тогда
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_TYPE, ОбъектДрайвера.LIBFPTR_PT_CREDIT);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_SUM, ЭтотОбъект.СуммаПостоплатой);
		ОбъектДрайвера.payment();
	КонецЕсли;
	// Иная
	Если ЭтотОбъект.СуммаВстречнымПредоставлением > 0 Тогда
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_TYPE, ОбъектДрайвера.LIBFPTR_PT_OTHER);
		ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_PAYMENT_SUM, ЭтотОбъект.СуммаВстречнымПредоставлением);
		ОбъектДрайвера.payment();
	КонецЕсли;
	
    // Закрытие чека
	ОбъектДрайвера.closeReceipt();
	Пока ОбъектДрайвера.checkDocumentClosed()<>0 Цикл
		
	КонецЦикла;
	
	ОшибкаТО = "";
	Если Не ОбъектДрайвера.getParamBool(ОбъектДрайвера.LIBFPTR_PARAM_DOCUMENT_CLOSED) Тогда
		ОшибкаТО = ОбъектДрайвера.errorDescription();
        // Документ не закрылся. Требуется его отменить (если это чек) и сформировать заново
        ОбъектДрайвера.cancelReceipt();
    КонецЕсли;
	
	// Смена
	ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_DATA_TYPE, ОбъектДрайвера.LIBFPTR_DT_SHIFT_STATE);
	ОбъектДрайвера.QueryData();
	ОбъектДрайвера_НомерСменыККМ = ОбъектДрайвера.getParamInt(ОбъектДрайвера.LIBFPTR_PARAM_SHIFT_NUMBER);
	
	// Запрос информации о закрытом чеке
	//ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_FN_DATA_TYPE, ОбъектДрайвера.LIBFPTR_FNDT_LAST_DOCUMENT);
	ОбъектДрайвера.setParam(ОбъектДрайвера.LIBFPTR_PARAM_FN_DATA_TYPE, ОбъектДрайвера.LIBFPTR_FNDT_LAST_RECEIPT);
	ОбъектДрайвера.fnQueryData();
	ОбъектДрайвера_НомерЧекаККМ = ОбъектДрайвера.getParamInt(ОбъектДрайвера.LIBFPTR_PARAM_DOCUMENT_NUMBER);
	
	ОшибкаТО = ОбъектДрайвера.errorDescription();
	Если ОшибкаТО = "Ошибок нет" Тогда
		ОшибкаТО = "";
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ОшибкаТО) Тогда
		Сообщить(ОшибкаТО);
		ОбъектДрайвера.close();
		ОбъектДрайвера = Неопределено;
		Возврат;
	Иначе
		НомерСменыККМ = ОбъектДрайвера_НомерСменыККМ;
		НомерЧекаККМ = ОбъектДрайвера_НомерЧекаККМ;
		ЧекПробитНаККМ = Истина;
	КонецЕсли;
	
	ОбъектДрайвера.close();
	ОбъектДрайвера = Неопределено;
	
	ПриЗаписиПерепроводить = Ложь;
	ЗаписатьВФорме(РежимЗаписиДокумента.Запись);
	ПриЗаписиПерепроводить = Истина;
	
КонецПроцедуры
